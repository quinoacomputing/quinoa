#!/bin/csh -f

# inherit PATH from cron_script
# setenv PATH /Net/local/gnu/bin:/bin:/usr/ucb:/usr/ccs/bin:/Net/local/bin:/usr/local/bin:$PATH

# todo: set appropriate repository revision?

#
# inherited from run_fast -- added to config.fast
# $CVSCMD Zoltan

# magic variables/names/numbers:
#   expects Zoltan directory, pre-checked out
#   if tag contains "smoke" it is a smoke test.
#   if tag contains "big" it is a big test (e.g. xyce) in Zoltan_Tests.  Zoltan_Tests is
#     expected to be in the zoltan user's home directory.
#   if $zdrive == zfdrive, do some stuff
#   if first argument $arch == --codecheck, do some different stuff


echo "Zoltan.build:  Starting..."
# fix issue with python in /usr/local
setenv LD_LIBRARY_PATH /usr/local/lib

# turn off globbing, to prevent expanding 'test/*' and similar args
set noglob

set dir=`pwd`
set fastdir="$dir/../../fast/client"
set driver="$fastdir/driver"

cd zoltan
set zdir=`pwd`

# from config.fast:  lin64test $tname zdrive 'test/{ch_*,hg_*}'
set arch="$1"
set tname="$2"
set zdrive="$3"
set testdirs="$4"
set bigtestdir="$5"	# optional

set tag='nightly'
echo "$tname" | grep 'smoke' > /dev/null && set tag='smoke'
echo "$tname" | grep 'big' > /dev/null && set bigtest


echo "Zoltan.build:  Configuration file..."
# configure output
cat << _DONE_ >& $dir/test/config.out
Config:
    ZOLTAN_ARCH=$arch
    tag=$tag
    Test name=$tname
    zdrive=$zdrive
    testdirs=$testdirs
_DONE_
echo "Zoltan.build:  Configuration file done."
# check other args here
if ("$arch" == "--codecheck") then
  $driver --o=$dir/test --I=$fastdir --name="$tname" check --config=test/th/codechecks.cfg

else

  # zoltan's configure and build are in one step, so FAST configure just prints variables
  echo "Zoltan.build:  Configuration ..."
  (cd src ; $driver --o=$dir/test --I=$fastdir --name="$tname" configure --logfile=$dir/test/config.out )
  echo "Zoltan.build:  Configuration done."

#  if ("$zdrive" == "zfdrive") then
#    # Special setup required for building zfdrive -- the F90 driver.
#    # Have to use appropriate zoltan_user_data.f90.
#    (cd src ; /bin/cp -p fdriver/zoltan_user_data.f90 fort/zoltan_user_data.f90)
#  endif

  echo "Zoltan.build:  gmake ..."
  (cd src ; gmake "ZOLTAN_ARCH=$arch" zoltan "$zdrive" >& $dir/test/build.out )
  echo "Zoltan.build:  gmake done."

  echo "Zoltan.build:  build ..."
  (cd src ; $driver --o=$dir/test --I=$fastdir --name="$tname" build --logfile=$dir/test/build.out )
  echo "Zoltan.build:  build done."

  echo "Zoltan.build:  mktests ..."
  if (! $?bigtest) then
    # create the test data, xml files, factor files
    ( cd test ; csh -f mktests "$zdrive" >& $dir/mktest.out )
  else if ($bigtestdir != "") then
    ( cd $bigtestdir ; setenv ZOLTAN_DIR $zdir ; csh -f mktests >& $dir/mktest.out )
  endif
  echo "Zoltan.build:  mktests done."

  # Test
  echo "Zoltan.build:  test ..."
  $driver --o=$dir/test --I=$fastdir --name="$tname" test --testdirs="$testdirs" --tag=$tag
  echo "Zoltan.build:  test done."

endif
